buildscript {
    dependencies {
        classpath 'org.flywaydb:flyway-mysql:9.11.0'
    }
}

plugins {
    id 'java'
    id 'jacoco-report-aggregation'
    id 'application'
    id 'org.springframework.boot' version '2.7.7'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.flywaydb.flyway' version '8.5.10'
}

group = 'com.fabricio.admin.catalogo.infrastructure'
version = '1.0-SNAPSHOT'

bootJar {
    archiveName 'application.jar'
    destinationDirectory.set(file("${rootProject.buildDir}/libs"))
}

repositories {
    mavenCentral()
}

dependencies {
    implementation(project(":domain"))
    implementation(project(":application"))

    implementation('com.google.cloud:google-cloud-storage:2.27.0')
    implementation('com.google.guava:guava:32.1.2-jre')

    implementation('io.vavr:vavr:0.10.4')

    implementation('mysql:mysql-connector-java')

    implementation('org.springdoc:springdoc-openapi-webmvc-core:1.6.14')
    implementation('org.springdoc:springdoc-openapi-ui:1.6.14')

    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude module: 'spring-boot-starter-tomcat'
    }
    implementation('org.springframework.boot:spring-boot-starter-undertow')
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.springframework.boot:spring-boot-starter-amqp')

    implementation('com.fasterxml.jackson.module:jackson-module-afterburner')

    testImplementation('org.flywaydb:flyway-core')
    testImplementation('org.flywaydb:flyway-mysql')

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.springframework.amqp:spring-rabbit-test:2.4.8')
    testImplementation('org.springframework.security:spring-security-test')

    testImplementation('org.testcontainers:testcontainers:1.17.6')
    testImplementation('org.testcontainers:mysql:1.17.6')
    testImplementation('org.testcontainers:junit-jupiter:1.17.6')

//    testImplementation('net.datafaker:datafaker:1.5.0')
    testImplementation(project(path: ':domain', configuration: 'testClasses'))

    testRuntimeOnly('com.h2database:h2')
}

flyway {
    url = System.getenv('FLYWAY_DB') ?: 'jdbc:mysql://localhost:3306/adm_videos'
    user = System.getenv('FLYWAY_USER') ?: 'root'
    password = System.getenv('FLYWAY_PASS') ?: 'root'
    cleanDisabled = false
}

testCodeCoverageReport {
    reports {
        xml {
            enabled true
            destination file("$rootDir/build/reports/jacoco/test/jacocoTestReport.xml")
        }

        html {
            enabled true
            destination file("$rootDir/build/reports/jacoco/test/")
        }
    }
}

tasks.named("jacocoTestReport") {
    dependsOn tasks.named("testCodeCoverageReport", JacocoReport)
}

//tasks.test {
//    useJUnitPlatform()
//}